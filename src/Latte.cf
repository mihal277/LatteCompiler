-- programs ------------------------------------------------

entrypoints Program ;

Program.   Program ::= [TopDef] ;

FnDef.     TopDef ::= Type PIdent "(" [Arg] ")" Block ;

ClassDef.  TopDef ::= ClassHeader "{" [ClassField] "}" ;

separator nonempty TopDef "" ;

Arg.       Arg ::= Type PIdent;

separator  Arg "," ;

-- classes -------------------------------------------------

ClassDec.        ClassHeader ::= "class" PIdent ;

ClassDecExt.     ClassHeader ::= "class" PIdent "extends" Type ;

ClassFieldVar.   ClassField ::= Type [Item] ";" ; 

ClassFieldMeth.  ClassField ::= Type PIdent "(" [Arg] ")" Block ;

separator        ClassField "" ;

-- statements ----------------------------------------------

Block.     Block ::= "{" [Stmt] "}" ;

separator  Stmt "" ;

Empty.     Stmt ::= ";" ;

BStmt.     Stmt ::= Block ;

Decl.      Stmt ::= Type [Item] ";" ;

NoInit.    Item ::= PIdent ; 

Init.      Item ::= PIdent "=" Expr ;

separator nonempty Item "," ;

Ass.       Stmt ::= PIdent "=" Expr  ";" ;

ArrAss.    Stmt ::= PIdent DimExpr "=" Expr  ";" ;

FieldAss.  Stmt ::= PIdent "." PIdent "=" Expr  ";" ;

Incr.      Stmt ::= PIdent "++"  ";" ;

Decr.      Stmt ::= PIdent "--"  ";" ;

Ret.       Stmt ::= "return" Expr ";" ;

VRet.      Stmt ::= "return" ";" ;

Cond.      Stmt ::= "if" "(" Expr ")" Stmt  ;

CondElse.  Stmt ::= "if" "(" Expr ")" Stmt "else" Stmt  ;

While.     Stmt ::= "while" "(" Expr ")" Stmt ;

For.       Stmt ::= "for" "(" Type PIdent ":" PIdent ")" Stmt ;

SExp.      Stmt ::= Expr  ";" ;

-- Types ---------------------------------------------------

Int.       Type ::= "int" ;

Str.       Type ::= "string" ;

Bool.      Type ::= "boolean" ;

Void.      Type ::= "void" ;

Arr.       Type ::= Type "[]" ;

ClassType. Type ::= PIdent ;

internal   Fun. Type ::= Type "(" [Type] ")" ;

separator  Type "," ;

-- Expressions ---------------------------------------------

EVar.      Expr6 ::= PIdent ;

ELitInt.   Expr6 ::= Integer ;

ELitTrue.  Expr6 ::= "true" ;

ELitFalse. Expr6 ::= "false" ;

EApp.      Expr6 ::= PIdent "(" [Expr] ")" ;

EAppMeth.  Expr6 ::= PIdent "." PIdent "(" [Expr] ")" ;

EObjVar.   Expr6 ::= PIdent "." PIdent ;

ENewArr.   Expr6 ::= "new" Type DimExpr;

ENewObj.   Expr6 ::= "new" PIdent;

EArrElem.  Expr6 ::= PIdent DimExpr;

EString.   Expr6 ::= String ;

ENull.     Expr6 ::= "(" PIdent ")null" ;

Neg.       Expr5 ::= "-" Expr6 ;

Not.       Expr5 ::= "!" Expr6 ;

EMul.      Expr4 ::= Expr4 MulOp Expr5 ;

EAdd.      Expr3 ::= Expr3 AddOp Expr4 ;

ERel.      Expr2 ::= Expr2 RelOp Expr3 ;

EAnd.      Expr1 ::= Expr2 "&&" Expr1 ;

EOr.       Expr ::= Expr1 "||" Expr ;

EDim.      DimExpr ::= "[" Expr "]" ;

coercions  Expr 6 ;

separator  Expr "," ;

-- operators -----------------------------------------------

Plus.      AddOp ::= "+" ;

Minus.     AddOp ::= "-" ;

Times.     MulOp ::= "*" ;

Div.       MulOp ::= "/" ;

Mod.       MulOp ::= "%" ;

LTH.       RelOp ::= "<" ;

LE.        RelOp ::= "<=" ;

GTH.       RelOp ::= ">" ;

GE.        RelOp ::= ">=" ;

EQU.       RelOp ::= "==" ;

NE.        RelOp ::= "!=" ;

-- comments ------------------------------------------------

comment    "#" ;

comment    "//" ;

comment    "/*" "*/" ;

-- tokens --------------------------------------------------

position token PIdent (letter (letter | digit | '_')*) ;
